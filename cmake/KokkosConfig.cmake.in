# No need for policy push/pop. CMake also manages a new entry for scripts
# loaded by include() and find_package() commands except when invoked with
# the NO_POLICY_SCOPE option
# CMP0057 + NEW -> IN_LIST operator in IF(...)
cmake_policy(SET CMP0057 NEW)

# Compute paths
@PACKAGE_INIT@

#Find dependencies
include(CMakeFindDependencyMacro)

#This needs to go above the KokkosTargets in case
#the Kokkos targets depend in some way on the TPL imports
@KOKKOS_TPL_EXPORTS@

get_filename_component(Kokkos_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include("${Kokkos_CMAKE_DIR}/KokkosTargets.cmake")
include("${Kokkos_CMAKE_DIR}/KokkosConfigCommon.cmake")
set(Kokkos_FOUND_INSTALL_DIR ${Kokkos_CMAKE_DIR}/../../..)
unset(Kokkos_CMAKE_DIR)

# components deprecation
if("launch_compiler" IN_LIST Kokkos_FIND_COMPONENTS)
  message(WARNING "'launch_compiler' is deprecated and will be removed in the future.")
elseif("separable_compilation" IN_LIST Kokkos_FIND_COMPONENTS)
  message(WARNING "'separable_compilation' is deprecated and will be removed in the future.")
endif()

if("launch_compiler" IN_LIST Kokkos_FIND_COMPONENTS AND "separable_compilation" IN_LIST Kokkos_FIND_COMPONENTS)
  message(STATUS "'launch_compiler' implies global redirection of targets depending on Kokkos to appropriate compiler.")
  message(
    STATUS
      "'separable_compilation' implies explicitly defining where redirection occurs via 'kokkos_compilation(PROJECT|TARGET|SOURCE|DIRECTORY ...)'"
  )
  message(FATAL_ERROR "Conflicting COMPONENTS: 'launch_compiler' and 'separable_compilation'")
endif()

if("launch_compiler" IN_LIST Kokkos_FIND_COMPONENTS)
  #
  # if find_package(Kokkos COMPONENTS launch_compiler) then rely on the
  # RULE_LAUNCH_COMPILE and RULE_LAUNCH_LINK to always redirect to the
  # appropriate compiler for Kokkos
  #

  message(
    STATUS
      "kokkos_launch_compiler is enabled globally. C++ compiler commands with -DKOKKOS_DEPENDENCE will be redirected to the appropriate compiler for Kokkos"
  )
  kokkos_compilation(GLOBAL CHECK_CUDA_COMPILES)
  set(Kokkos_GLOBAL_COMPILATION OFF)
endif()
# end of components deprecation

if(NOT DEFINED Kokkos_GLOBAL_COMPILATION)
  set(Kokkos_GLOBAL_COMPILATION ON
      CACHE BOOL "Whether Kokkos sets source and target properties globally via a deferred call"
  )
endif()

if(Kokkos_GLOBAL_COMPILATION)

  cmake_language(
    EVAL
    CODE
    "
    cmake_language(DEFER DIRECTORY ${PROJECT_SOURCE_DIR} CALL kokkos_set_source_and_target_properties PROJECT Kokkos_ROOT ${Kokkos_FOUND_INSTALL_DIR})
    "
  )

  unset(Kokkos_FOUND_INSTALL_DIR)
endif()

set(Kokkos_COMPILE_LANGUAGE @KOKKOS_COMPILE_LANGUAGE@)

# No need for policy push/pop. CMake also manages a new entry for scripts
# loaded by include() and find_package() commands except when invoked with
# the NO_POLICY_SCOPE option
# CMP0057 + NEW -> IN_LIST operator in IF(...)
cmake_policy(SET CMP0057 NEW)

# Compute paths
@PACKAGE_INIT@

#Find dependencies
include(CMakeFindDependencyMacro)

#This needs to go above the KokkosTargets in case
#the Kokkos targets depend in some way on the TPL imports
@KOKKOS_TPL_EXPORTS@

get_filename_component(Kokkos_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include("${Kokkos_CMAKE_DIR}/KokkosTargets.cmake")
include("${Kokkos_CMAKE_DIR}/KokkosConfigCommon.cmake")
unset(Kokkos_CMAKE_DIR)

# check for conflicts
if("launch_compiler" IN_LIST Kokkos_FIND_COMPONENTS AND "separable_compilation" IN_LIST Kokkos_FIND_COMPONENTS)
  message(STATUS "'launch_compiler' implies global redirection of targets depending on Kokkos to appropriate compiler.")
  message(
    STATUS
      "'separable_compilation' implies explicitly defining where redirection occurs via 'kokkos_compilation(PROJECT|TARGET|SOURCE|DIRECTORY ...)'"
  )
  message(FATAL_ERROR "Conflicting COMPONENTS: 'launch_compiler' and 'separable_compilation'")
endif()

if(NOT @Kokkos_ENABLE_COMPILE_AS_CMAKE_LANGUAGE)
  #we compile a small test program using kokkos to check if the flags and ABI the CXX compiler uses is compatible with the library
  kokkos_check_cxx_compiler_is_compatible()

  if((@KOKKOS_ENABLE_CUDA@ AND NOT KOKKOS_CXX_COMPILER_IS_COMPATIBLE) OR "launch_compiler" IN_LIST
                                                                         Kokkos_FIND_COMPONENTS
  )
    #
    # if find_package(Kokkos COMPONENTS launch_compiler) then rely on the
    # RULE_LAUNCH_COMPILE and RULE_LAUNCH_LINK to always redirect to the
    # appropriate compiler for Kokkos
    #

    message(
      STATUS
        "kokkos_launch_compiler is enabled globally. C++ compiler commands with -DKOKKOS_DEPENDENCE will be redirected to the appropriate compiler for Kokkos"
    )
    kokkos_compilation(GLOBAL)

  else()
    if(NOT KOKKOS_CXX_COMPILER_IS_COMPATIBLE)
      message(
        WARNING
          "Kokkos can not compile a simple test program with the given CXX compiler ${CMAKE_CXX_COMPILER}. This might be due to incompatible flags or ABI."
      )
    endif()
  endif()
endif()

set(Kokkos_COMPILE_LANGUAGE @KOKKOS_COMPILE_LANGUAGE@)
